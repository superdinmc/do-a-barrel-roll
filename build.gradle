plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.

    // Personal maven for cicada and backup mirrors of some dependencies.
    maven { url = "https://maven.enjarai.nl/mirrors" }
    maven { url = "https://maven.enjarai.nl/releases" }

    maven {
        url = "https://www.cursemaven.com"
        allowInsecureProtocol = true
    }

    // Mod Menu.
    maven { url 'https://maven.terraformersmc.com' }

    // MidnightControls.
    maven { url = "https://aperlambda.github.io/maven" }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }

    // Cloth Config.
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases/" }

    // Jitpack for Mixin Extras.
    maven {
        url 'https://jitpack.io'
        content {
            includeGroup 'com.github.llamalad7'
        }
    }

    // YACL/Controlify
    maven { url = "https://maven.isxander.dev/releases" }
    maven { url = "https://maven.quiltmc.org/repository/release/" }

    // Cardinal Components API
    maven {
        name = "Ladysnake Mods"
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }

    // Cloth Config
    maven { url "https://maven.shedaniel.me/" }

    // Mod Menu
    maven { url "https://maven.terraformersmc.com/releases" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include modImplementation("nl.enjarai:cicada-lib:${project.cicada_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    modImplementation("dev.isxander:yet-another-config-lib:${project.yacl_version}") {
        exclude group: "net.fabricmc"
    }

    modImplementation("dev.isxander:controlify:${project.controlify_version}:fabric-1.19.4") {
        exclude group: "net.fabricmc"
        exclude group: "maven.modrinth"
    }

    // Mod Menu integration. Currently unused.
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    // Cloth Config for easy config menu.
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude group: "net.fabricmc"
    }

    // Mixin Extras for extra compatibility.
    include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}")))

    // Cardinal Components API for storing data server-side.
    include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}")
    include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_components_version}")
}

base {
    archivesName = project.archives_base_name
}

version = "${rootProject.mod_version}+${rootProject.minecraft_version}"

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}"}
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 17
}

// configure the maven publication
publishing {
    repositories {
        maven {
            name = "enjaraiMaven"
            url = "https://maven.enjarai.nl/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.maven_group
            artifactId = archivesBaseName
            version = project.version

            from components.java
        }
    }
}
